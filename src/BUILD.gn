# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build_overrides/ipcz.gni")
import("//testing/test.gni")

source_set("ipcz") {
  sources = [ "api/api.cc" ]
  public_deps = [ ":ipcz_sources" ]
  deps = [ "//third_party/abseil-cpp:absl" ]
  public_configs = [ ":ipcz_include_dir" ]
  configs += [ ":ipcz_include_src_dir" ]
}

shared_library("ipcz_shared") {
  output_name = "ipcz"
  sources = [ "api/api.cc" ]
  public_deps = [ ":ipcz_sources" ]
  deps = [ "//third_party/abseil-cpp:absl" ]
  defines = [ "IPCZ_SHARED_LIBRARY" ]
  public_configs = [ ":ipcz_include_dir" ]
  configs += [ ":ipcz_include_src_dir" ]
}

source_set("ipcz_header") {
  visibility = [ ":*" ]
  public = [ "include/ipcz/ipcz.h" ]
  public_configs = [ ":ipcz_include_dir" ]
}

source_set("ipcz_header_types") {
  public = [ "include/ipcz/ipcz.h" ]
}

source_set("support") {
  visibility = [ ":*" ]

  public = [
    "debug/hex_dump.h",
    "debug/log.h",
    "debug/stack_trace.h",
    "mem/atomic_memcpy.h",
    "mem/block_allocator.h",
    "mem/mpmc_queue.h",
    "mem/ref_counted.h",
    "os/channel.h",
    "os/event.h",
    "os/handle.h",
    "os/memory.h",
    "os/process.h",
    "util/handle_util.h",
    "util/mutex_locks.h",
    "util/random.h",
  ]

  sources = [
    "debug/hex_dump.cc",
    "debug/stack_trace.cc",
    "mem/atomic_memcpy.cc",
    "mem/block_allocator.cc",
    "mem/ref_counted.cc",
    "os/channel.cc",
    "os/event.cc",
    "os/handle.cc",
    "os/memory.cc",
    "os/process.cc",
    "util/mutex_locks.cc",
    "util/random.cc",
  ]

  deps = [ "//third_party/abseil-cpp:absl" ]
  public_deps = [ ":ipcz_header" ]

  public_configs = [ ":ipcz_include_dir" ]
  configs += [ ":ipcz_include_src_dir" ]

  if (build_ipcz_standalone) {
    public_deps += [ "${ipcz_src_root}/standalone" ]
    public_configs += [ ":ipcz_standalone_config" ]
  }
}

source_set("core") {
  visibility = [ ":*" ]

  sources = [
    "core/block_allocator_pool.cc",
    "core/block_allocator_pool.h",
    "core/buffer_id.h",
    "core/driver_memory.cc",
    "core/driver_memory.h",
    "core/driver_memory_mapping.cc",
    "core/driver_memory_mapping.h",
    "core/driver_transport.cc",
    "core/driver_transport.h",
    "core/link_side.cc",
    "core/link_side.h",
    "core/link_type.cc",
    "core/link_type.h",
    "core/local_router_link.cc",
    "core/local_router_link.h",
    "core/message_internal.cc",
    "core/message_internal.h",
    "core/message_macros/message_declaration_macros.h",
    "core/message_macros/message_definition_macros.h",
    "core/message_macros/message_dispatch_macros.h",
    "core/message_macros/message_params_declaration_macros.h",
    "core/message_macros/message_params_definition_macros.h",
    "core/message_macros/message_reply_dispatch_macros.h",
    "core/message_macros/undef_message_macros.h",
    "core/node.cc",
    "core/node.h",
    "core/node_connector.cc",
    "core/node_connector.h",
    "core/node_link.cc",
    "core/node_link.h",
    "core/node_link_address.cc",
    "core/node_link_address.h",
    "core/node_link_memory.cc",
    "core/node_link_memory.h",
    "core/node_message_defs.h",
    "core/node_messages.cc",
    "core/node_messages.h",
    "core/node_name.cc",
    "core/node_name.h",
    "core/parcel.cc",
    "core/parcel.h",
    "core/parcel_queue.cc",
    "core/parcel_queue.h",
    "core/portal.cc",
    "core/portal.h",
    "core/remote_router_link.cc",
    "core/remote_router_link.h",
    "core/route_edge.cc",
    "core/route_edge.h",
    "core/router.cc",
    "core/router.h",
    "core/router_descriptor.cc",
    "core/router_descriptor.h",
    "core/router_link.h",
    "core/router_link_state.cc",
    "core/router_link_state.h",
    "core/router_tracker.cc",
    "core/router_tracker.h",
    "core/sequence_number.h",
    "core/sublink_id.h",
    "core/trap.cc",
    "core/trap.h",
    "core/trap_event_dispatcher.cc",
    "core/trap_event_dispatcher.h",
    "core/trap_set.cc",
    "core/trap_set.h",
  ]

  deps = [ "//third_party/abseil-cpp:absl" ]
  public_deps = [
    ":ipcz_header",
    ":support",
  ]

  public_configs = [ ":ipcz_include_dir" ]
  configs += [ ":ipcz_include_src_dir" ]
}

source_set("ipcz_sources") {
  deps = [
    ":core",
    ":support",
  ]

  public_deps = [ ":ipcz_header" ]

  public_configs = [ ":ipcz_include_dir" ]
}

source_set("drivers") {
  sources = [
    "drivers/multiprocess_reference_driver.cc",
    "drivers/multiprocess_reference_driver.h",
    "drivers/single_process_reference_driver.cc",
    "drivers/single_process_reference_driver.h",
  ]

  deps = [
    ":support",
    "//third_party/abseil-cpp:absl",
  ]
  public_deps = [ ":ipcz_header" ]

  configs += [ ":ipcz_include_src_dir" ]
}

config("ipcz_include_dir") {
  include_dirs = [ "${ipcz_include_root}" ]
}

config("ipcz_include_src_dir") {
  include_dirs = [ "${ipcz_src_root}" ]
}

config("ipcz_standalone_config") {
  include_dirs = [ "${ipcz_src_root}/standalone" ]
}

test("ipcz_tests") {
  sources = [
    "api/api_tests/arm_trap_api_test.cc",
    "api/api_tests/begin_get_api_test.cc",
    "api/api_tests/begin_put_api_test.cc",
    "api/api_tests/close_portal_api_test.cc",
    "api/api_tests/connect_node_api_test.cc",
    "api/api_tests/create_node_api_test.cc",
    "api/api_tests/create_trap_api_test.cc",
    "api/api_tests/destroy_node_api_test.cc",
    "api/api_tests/destroy_trap_api_test.cc",
    "api/api_tests/end_get_api_test.cc",
    "api/api_tests/end_put_api_test.cc",
    "api/api_tests/get_api_test.cc",
    "api/api_tests/merge_portals_api_test.cc",
    "api/api_tests/open_portals_api_test.cc",
    "api/api_tests/put_api_test.cc",
    "api/api_tests/query_portal_status_api_test.cc",
    "api/integration_tests/connect_test.cc",
    "api/integration_tests/merge_portals_test.cc",
    "api/integration_tests/node_test.cc",
    "api/integration_tests/portal_transfer_test.cc",
    "api/integration_tests/remote_portal_test.cc",
    "api/integration_tests/trap_test.cc",
    "core/parcel_queue_test.cc",
    "mem/block_allocator_test.cc",
    "mem/mpmc_queue_test.cc",
    "mem/ref_counted_test.cc",
    "os/channel_test.cc",
    "os/event_test.cc",
    "os/memory_test.cc",
    "os/multiprocess_support_test.cc",
    "test/api_test.cc",
    "test/api_test.h",
    "test/multinode_test.cc",
    "test/multinode_test.h",
    "test/multiprocess_test.cc",
    "test/multiprocess_test.h",
    "test/run_all_tests.cc",
    "test/test_base.cc",
    "test/test_base.h",
    "test/test_client.cc",
    "test/test_client.h",
  ]

  deps = [
    ":core",
    ":drivers",
    ":ipcz",
    "//testing/gtest",
    "//third_party/abseil-cpp:absl",
    "//third_party/abseil-cpp/absl/flags:flag",
    "//third_party/abseil-cpp/absl/flags:parse",
  ]

  configs += [ ":ipcz_include_src_dir" ]
}

group("all") {
  testonly = true
  deps = [
    ":ipcz",
    ":ipcz_shared",
    ":ipcz_tests",
  ]
}
